{-# LANGUAGE ScopedTypeVariables #-}

module Portage.EBuild.Golden (goldenTests) where

import qualified Data.ByteString.Lazy as LBS
import qualified Data.Text.Lazy as T
import qualified Data.Text.Lazy.Encoding as LBS
import Data.Time.Clock (UTCTime)
import System.FilePath (takeBaseName, replaceExtension)
import Test.Tasty (TestTree, testGroup)
import Test.Tasty.Golden (goldenVsStringDiff, findByExtension)

import Portage.EBuild (showEBuild)
import Paths_hackport (getDataFileName)

goldenTests :: IO TestTree
goldenTests = do
    dataDir <- getDataFileName "tests/data/golden/"
    -- @.out@ files were generated by 'pretty-simple' outputting live 'EBuild's
    outFiles <- findByExtension [".out"] dataDir
    pure $ testGroup "hackport golden tests" $ map runTest outFiles
  where
    runTest :: FilePath -> TestTree
    runTest outFile =
        goldenVsStringDiff
            (takeBaseName outFile) -- test name
            (\ref new -> ["/usr/bin/diff", "-u", "--color=always", ref, new])
            (replaceExtension outFile ".ebuild") -- golden file path
            (readTestFile outFile) -- action whose result is tested

    readTestFile :: FilePath -> IO LBS.ByteString
    readTestFile =
        fmap (LBS.encodeUtf8 . T.pack . showEBuild time2023 . read) . readFile

    time2023 :: UTCTime
    time2023 = read "2023-01-03 00:00:00 UTC"
